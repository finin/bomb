PREFIX dbo: <http://dbpedia.org/ontology/>
PREFIX foaf: <http://xmlns.com/foaf/0.1/>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
SELECT ?Per ?Wiki ?Name (count(?X) as ?Inlinks) ?Text ?PicUrl ?Date
WHERE {
  ?Per a dbo:Person;
     dbo:birthDate ?Date;
     rdfs:label ?Name;
     # DBpedia's comment is the first few sentences of the article
     rdfs:comment ?Text.
  # must have a date that matches
  FILTER (regex(?Date, "%s"))  # REPLACE WITH VALUE
  # we only want Name and Text in English
  FILTER (langmatches(lang(?Text), "EN")  && langmatches(lang(?Name), "EN"))
  # a picture is optional
  OPTIONAL {?Per foaf:depiction ?PicUrl}
  ?Wiki foaf:primaryTopic ?Per.
  # find incoming links (must be at least one) to the person so we can
  # sort by importance
  ?X ?R ?Per.
 }
ORDER BY DESC(?Inlinks)
LIMIT 20
OFFSET %s    # REPLACE WITH VALUE

PREFIX dbo: <http://dbpedia.org/ontology/>
PREFIX foaf: <http://xmlns.com/foaf/0.1/>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
SELECT ?Per (sample(?Wiki) as ?W) (sample(?Name) as ?N) (count(?X) as ?Inlinks) (sample(?abs) as ?t) (sample(?PicUrl) as ?Pic) (sample(?Date) as ?D)
WHERE {
  ?Per a dbo:Person;
     dbo:birthDate ?Date;
     rdfs:label ?Name;
     dbo:abstract ?abs.
  # must have a date that matches
  FILTER (regex(?Date, "%s"))  # REPLACE WITH VALUE
  # Only want English strings
  FILTER (langmatches(lang(?abs), "EN")  && langmatches(lang(?Name), "EN"))
  # Picture is optional
  OPTIONAL {?Per foaf:depiction ?PicUrl}
  ?Wiki foaf:primaryTopic ?Per.
  # find incoming links to person to sort by importance
  ?X ?R ?Per.
 }
GROUP BY ?Per
ORDER BY DESC(?Inlinks)
LIMIT 20
OFFSET %s    # REPLACE WITH VALUE
